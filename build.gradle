apply from:'./gradle/maven-central-build.gradle'

configurations {
    provided
}

ext {
    grailsVersion = '2.4.1.BUILD-SNAPSHOT'
    groovyVersion = '2.3.2'
    datastoreVersion = '3.1.1.RELEASE'
}

group = 'org.grails'
version = "1.0-grails-2.4"
sourceCompatibility = 1.6
targetCompatibility = 1.6

apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    testCompile "junit:junit:4.11"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        transitive = false
    }
    provided "org.codehaus.groovy:groovy-all:$groovyVersion"
    provided "org.grails:grails-core:$grailsVersion"
    provided "org.grails:grails-bootstrap:$grailsVersion"
    provided "org.grails:grails-plugin-testing:$grailsVersion"
    provided "org.grails:grails-datastore-gorm:$datastoreVersion"
    provided "org.grails:grails-datastore-gorm-hibernate4:$datastoreVersion"
    provided "org.grails:grails-datastore-core:$datastoreVersion"
    provided "javax.servlet:javax.servlet-api:3.0.1"
    provided "junit:junit:4.11"
    provided "org.springframework:spring-orm:4.0.5.RELEASE"
    provided "com.h2database:h2:1.3.173"
}

sourceSets.main.compileClasspath += configurations.provided
sourceSets.test.compileClasspath = configurations.provided + sourceSets.test.compileClasspath
sourceSets.test.runtimeClasspath = configurations.provided + sourceSets.test.runtimeClasspath
javadoc.classpath += configurations.provided

idea {
  module{
    scopes.PROVIDED.plus += configurations.provided
  }
}
eclipse {
  classpath {
    plusConfigurations += configurations.provided
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}